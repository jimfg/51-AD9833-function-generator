	;USE THE SEQUENCE IN ANALOG DEVICES APPLICATION NOTE AN-1070
	;IF THE MODULE IS RIGHT, A 400HZ SINEWAVE WILL BE GENERATED

	;(OUTPUT) AD9833
	.EQU	SCLK, 90H	;P1.0
	.EQU	SDATA, 91H	;P1.1
	.EQU	FSYNC, 92H	;P1.2

	;MAIN: CALL SENDW TO SEND THE 5 WORDS TO MAKE A 400HZ SINEWAVE
MAIN:	MOV	R7, #21H
	MOV	R6, #00H
	ACALL	SENDW
	MOV	R7, #50H
	MOV	R6, #0C7H
	ACALL	SENDW
	MOV	R7, #40H
	MOV	R6, #00H
	ACALL	SENDW
	MOV	R7, #0C0H
	MOV	R6, #00H
	ACALL	SENDW
	MOV	R7, #20H
	MOV	R6, #00H
	ACALL	SENDW
HERE:	SJMP	HERE

	;SENDW: SEND A 16-BIT WORD TO AD9833
 	;PARAM: THE WORD IS IN R7R6, R7.7 SHOULD FIRST BE SENT
SENDW:	CLR	FSYNC
	MOV	A, R7
	MOV	R5, #8
SHIFT1:	RLC	A
	MOV	SDATA, C
	CLR	SCLK
	SETB	SCLK
	DJNZ	R5, SHIFT1
	MOV	A, R6
	MOV	R5, #8
SHIFT2:	RLC	A
	MOV	SDATA, C
	CLR	SCLK
	SETB	SCLK
	DJNZ	R5, SHIFT2
	SETB	FSYNC
	RET
	
