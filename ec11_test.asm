	;whether ec11 can work with this method...
	.EQU	ROTA, 90H	;P1.0
	.EQU	ROTB, 91H
	.EQU	LOOK, 97H

	LJMP	MAIN
	.ORG	1BH
	LJMP	TIMER1
	
	.ORG	30H
	;MAIN: KEEPS CALLING GETF, SHOW AT LOOK
MAIN:	MOV	R7, #0
	CLR	00H
	CLR	01H
	MOV	TMOD, #10H	;TIMER1 MODE1
	MOV	IE, #88H
	MOV	TL1, #1AH
	MOV	TH1, #0FFH
C_GETF:	ACALL	GETF
	SJMP	C_GETF

	;GETF: GET FREQUENCY CHANGE INFO FROM EC11 ROTARY ENCODER
	;(TOGETHER WITH TIMER1 INTERRUPT HANDLER:)
	;MEM/REG: R7, BIT 00H, BIT 01H	BOOKKEEPING
	;	  TF1, TR1, TL1, TH1	TIMING
	;	  R6 			RESULT(-1, 0, +1)
	;	  ROTA, ROTB		INPUTS
GETF:	JNB	01H, CONT0
	RET
CONT0:	CJNE	R7, #0, ROT_X
	JB	ROTA, CONT1
	MOV	R7, #1
	RET
CONT1:	JB	ROTB, RET0
	MOV	R7, #2
RET0:	RET
ROT_X:	CJNE	R7, #1, ROT_B
	;ROT_A: COUNTERCLOCKWISE, ROT_B: CLOCKWISE
ROT_A:	JB	00H, CONT2
	JB	ROTB, RET1
	SJMP	TR_RET1
CONT2:	JNB	ROTB, RET1
	SJMP	TR_RET1
ROT_B:	JB	00H, CONT3
	JB	ROTA, RET1
	SJMP	TR_RET1
CONT3:	JNB	ROTA, RET1
TR_RET1:SETB	TR1
	SETB	01H
RET1:	RET
	;TIMER1: MODIFY R6 ACCORDING TO BOOKKEEPING INFO AND ROTA, ROTB
	;WHEN JUMP HERE, TF1 IS CLEARED
TIMER1:	CLR	TR1
	MOV	TL1, #1AH
	MOV	TH1, #0FFH	;ABOUT 250US TO OVERFLOW
	CLR	01H
	MOV	R6, #0
	JB	00H, CONT4
	SETB	00H
	RETI
CONT4:	CJNE	R7, #1, CHECK_B
CHECK_A:JB	ROTA, CONT5
	MOV	R7, #2
	RETI	
CONT5:	MOV	R6, #255
	ACALL	LOOK0
	SJMP	FI_RET2
CHECK_B:JB	ROTB,CONT6
	MOV	R7, #1
	RETI
CONT6:	MOV	R6, #1
	ACALL	LOOK1
FI_RET2:MOV	R7, #0
	CLR	00H
	RETI

	;DELAY: DELAY ABOUT 100US
DELAY:	MOV	R5, #50
HERE:	DJNZ	R5, HERE
	RET
LOOK0:	CLR	LOOK
	ACALL	DELAY
	SETB	LOOK
	RET
LOOK1:	CLR	LOOK
	ACALL	DELAY
	ACALL	DELAY
	SETB	LOOK
	RET

;there are times when rotate the rotary encoder, oscilloscope does
;not show pulse of both ROTA and LOOK, cannot figure out why now
;not familiar enough with the scope
;mechanical thing with rotary encoder
;code is not good
;circuit connection is not good
;...
